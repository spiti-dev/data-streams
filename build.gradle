plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'dev.spiti.utilities'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.3.0'
    implementation group: 'com.google.guava', name: 'guava', version: '24.1-jre'
    testImplementation group: 'org.testng', name: 'testng', version: '6.14.3'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'data-streams'
            from components.java
            artifact sourcesJar
            //artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Data Streams'
                description = 'A utility to read and write data from and to kafka'
                url = 'https://github.com/spiti-dev/data-streams'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name  = 'Naresh Gourishetty'
                        email = 'naresh.g@spititech.com'
                    }
                    developer {
                        name = 'spiti.dev'
                        email = 'contact@spiti.dev'
                    }
                }
                scm {
                    url = 'https://github.com/spiti-dev/data-streams'
                    connection = 'scm:git:git://github.com/spiti-dev/data-streams.git'
                    developerConnection = 'scm:git:ssh://github.com/spiti-dev/data-streams.git'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
